terminal BRACKETL, BRACKETR, CURLYBRACEL, CURLYBRACER, SEMICOLON, PERIOD, COMMA;
terminal CLASS, ELSE, EXTENDS, IF, NEW, NULL, RETURN, VOID, SUPER;
terminal ASSIGN, AND, DIV, EQ, GT, GTE, MINUS, MOD, MULT, NOT, OR, PLUS;
terminal IDENT, LITINT, LITCHAR, LITLONG, LITDOUBLE, LITBOOL;
terminal INT, CHAR, LONG, DOUBLE, BOOL;

non terminal lit, numtype, primtype, type, infixop, block;
non terminal name, simplename, qualifiedname;

non terminal program;
non terminal classdef, classdecl, classbody, classbodydecls, classbodydecl, classdeflist;
non terminal constructordef, constructordecl, constructorbody;
non terminal statement, statementlist;
non terminal param, paramlist, paramlistcomma;
non terminal arglist;
non terminal fielddef, localvardef, ifelse;
non terminal methoddef, methoddecl, methodheader;
non terminal expression, promexpression, opexpression, castexpression, unaryexpression;
non terminal varassignment, primary, fieldaccess;

// Precedences
precedence left OR, AND;
precedence left EQ;
precedence left GTE, GT;
precedence left PLUS, MINUS;
precedence left MULT, DIV, MOD;
precedence left NOT;

start with program;

// Simple types
numtype ::= INT | CHAR | LONG | DOUBLE;
primtype ::= numtype | BOOL;
type ::= name | primtype;

// Low-level expressions
lit ::= LITINT | LITCHAR | LITLONG | LITDOUBLE | LITBOOL | NULL;
infixop ::= PLUS | MINUS | MULT | DIV
    | MOD | GT | GTE | AND | OR | EQ;

// Names
name ::= simplename | qualifiedname;
simplename ::= IDENT:id;
qualifiedname ::= name PERIOD IDENT:id;

program ::= classdeflist;

// Classes
classdef ::=  classdecl classbody;
classdecl ::= CLASS IDENT:classname EXTENDS name;
classbody ::= CURLYBRACEL classbodydecls CURLYBRACER
    | CURLYBRACEL CURLYBRACER;
classbodydecls ::= classbodydecl
    | classbodydecls classbodydecl;
classbodydecl ::= fielddef
    | methoddef
    | constructordef;

classdeflist ::= classdef
    | classdef:cd classdeflist:cdl;

// Fields
fielddef ::= type simplename SEMICOLON;
localvardef ::= type simplename;

// Parameters
param ::= type:type IDENT:id;
paramlist ::= param
    | param:param paramlistcomma;
paramlistcomma ::= COMMA paramlist;

// Args
arglist ::= expression
    | arglist COMMA expression;

// Constructor
constructordef ::= constructordecl constructorbody;
constructordecl ::= IDENT:id BRACKETL paramlist BRACKETR
    | IDENT:id BRACKETL BRACKETR;
constructorbody ::= CURLYBRACEL SUPER BRACKETL arglist BRACKETR SEMICOLON CURLYBRACER
    | CURLYBRACEL SUPER BRACKETL BRACKETR SEMICOLON CURLYBRACER;

// Method
methoddef ::= methodheader block;
methodheader ::= type:type methoddecl
    | VOID methoddecl;
methoddecl ::= IDENT:id BRACKETL paramlist BRACKETR
    | IDENT:id BRACKETL BRACKETR;

block ::= CURLYBRACEL CURLYBRACER
    | CURLYBRACEL statementlist CURLYBRACER;

// Statements
statementlist ::= statement
    | statement statementlist;
statement ::= SEMICOLON
    | localvardef SEMICOLON
    | promexpression SEMICOLON
    | ifelse
    | varassignment SEMICOLON
    | RETURN expression SEMICOLON
    | block;

varassignment ::= name ASSIGN expression;

ifelse ::= IF BRACKETL expression EQ expression BRACKETR block ELSE block;

// Simplified version from https://www.cs.cornell.edu/andru/javaspec/19.doc.html
expression ::= opexpression;

opexpression ::= opexpression infixop opexpression
    | castexpression;

// Whether or not this is a valid cast must be decided later
// due to LALR(1) limitations
castexpression ::= BRACKETL expression BRACKETR opexpression
    | BRACKETL primtype BRACKETR opexpression
    | unaryexpression;

unaryexpression ::= NOT unaryexpression
    | primary
    | name;

primary ::= lit
    | BRACKETL expression BRACKETR
    | fieldaccess;

fieldaccess ::= primary PERIOD IDENT;
